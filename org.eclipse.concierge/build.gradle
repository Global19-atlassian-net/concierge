// TODO align directory structure to Maven based one
sourceSets {
    main {
        resources {
            srcDirs = [ "." ]
			include "META-INF/services/**", "about.html", "about_files/**"
        }
    }
}

jar {
    manifest {
		from('META-INF/MANIFEST.MF')
        attributes 'Bundle-Version': version
        attributes 'Implementation-Title': 'Eclipse Concierge OSGi R5 Framework',
 			       'Implementation-Version': version
	}
}

// make stripped jar with no debug information
// pack200 --repack --strip-debug <stripped-jar> <original-jar>
task strippedJar(type: Exec, dependsOn: jar) {
    def inputFile = new File(jar.destinationDir, jar.archiveName)
    def outputFile = new File(jar.destinationDir, jar.archiveName.replace('.jar', '-nodebug.jar'))

	outputs.files outputFile

    commandLine 'pack200', '--repack', '--strip-debug', outputFile.absolutePath, inputFile.absolutePath
}

publishing {
    publications {
   	    mavenJava(MavenPublication) {
	   	    groupId 'org.eclipse.concierge'
            artifactId 'org.eclipse.concierge'
            version version
       	    from components.java
	       	artifact sourceJar {
                classifier "sources"
            }
       	}
   	}
}
